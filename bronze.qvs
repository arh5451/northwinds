/*
	Extract Script

	Changelog:
	- 25.7.25 - First edit, initial load of xls data
*/

//Load subscripts
$(Must_Include=lib://main/scripts/subs.qvs);

//Flat file extraction location
SET vNorthwindUnclean = 'lib://main/rawdata/Qlik Northwind_Uncleaned.xls';

//SQL or othe SAP source
SET vSQL = '';
SET vSAP = '';

//Set the extraction source
LET vExtractSource = '$(vNorthwindUnclean)';

//List the tables you want to extract
[ExtractTablesList]:
LOAD * INLINE [
TableNames
Categories$
Customers$
Employees$
Orders Details$
Orders$
Product$
Shippers$
Suppliers$
]
;

//Set a cutoff date to start the extract from. - not used in this app
// SET vMinDate = 2018;

//Loop Through List of tables and store as QVD with no changes.
FOR EACH i in FieldValueList('TableNames')

    //Excel Extraction Loop
    [$(i)]:
    LOAD
        *
    FROM [$(vExtractSource)]
    (biff, embedded labels, table is [$(i)]);
    //WHERE YEAR(___) > $(vMinDate) - not in use ignore this

    TRACE $(i);

    Call rcount('$(i)');

    [MetaDataTable]:
    LOAD
      '$(i)'              as [TableName]
      , $(vRowCount)      as [RowCount]
      , 'bronze'          as [Layer]
    AUTOGENERATE (1);

    STORE [$(i)] INTO [lib://main/bronze/$(i).qvd] (qvd);

    DROP TABLE [$(i)];


NEXT i

STORE MetaDataTable INTO [lib://main/metadata/MetaDataTable.qvd] (qvd);